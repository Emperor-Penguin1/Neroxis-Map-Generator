import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '2.0.4'
}

group 'net.neroxis'
version "${version}"

sourceCompatibility = JavaVersion.VERSION_1_8

sourceSets {
    main {
        java {
            srcDirs = ['src/java']
        }
        resources {
            srcDirs = ['src/resources']
        }
    }

    test {
        java {
            srcDirs = ['src/test/java']
        }
    }
}


repositories {
    maven { url "https://jitpack.io" }
    mavenCentral()
}

dependencies {
    implementation("com.github.FAForever:faf-java-commons:0b83fa48a039cea085a7d462cc937fb5a035c186") {
        exclude module: 'guava'
        exclude module: 'faf-commons-api'
    }
    implementation("org.luaj:luaj-jse:3.0.1")
    implementation("com.google.guava:guava:28.0-jre")
    compileOnly('org.projectlombok:lombok:1.18.14')
    annotationProcessor('org.projectlombok:lombok:1.18.14')
    implementation('com.google.code.gson:gson:2.8.5')

    testCompile('junit:junit:4.11')
}

shadowJar {
    getArchiveFileName().set("NeroxisGen_${generatorVersion}.jar")
    manifest {
        attributes 'Main-Class': 'neroxis.generator.MapGenerator',
                "Implementation-Version": generatorVersion,
                "Impletementation-Title": "Neroxis Map Generator"
    }
}

task populatorJar(type: ShadowJar) {
    getArchiveBaseName().set('MapPopulator')
    from(sourceSets.main.output) {
        manifest {
            attributes 'Main-Class': 'neroxis.populator.MapPopulator'
        }
    }
    project.configurations.implementation.canBeResolved = true
    configurations = [project.configurations.implementation]
}

task evaluatorJar(type: ShadowJar) {
    getArchiveBaseName().set('MapEvaluator')
    from(sourceSets.main.output) {
        manifest {
            attributes 'Main-Class': 'neroxis.evaluator.MapEvaluator'
        }
    }
    project.configurations.implementation.canBeResolved = true
    configurations = [project.configurations.implementation]
}

task transformerJar(type: ShadowJar) {
    getArchiveBaseName().set('MapTransformer')
    from(sourceSets.main.output) {
        manifest {
            attributes 'Main-Class': 'neroxis.transformer.MapTransformer'
        }
    }
    project.configurations.implementation.canBeResolved = true
    configurations = [project.configurations.implementation]
}